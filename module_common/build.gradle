import com.lepucare.xphealth.build.*

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.protobuf'


android {
    compileSdkVersion BuildConfig.compileSdkVersion
    buildToolsVersion BuildConfig.buildToolsVersion

    defaultConfig {
        minSdkVersion BuildConfig.minSdkVersion
        targetSdkVersion BuildConfig.targetSdkVersion
        versionCode BuildConfig.versionCode
        versionName BuildConfig.versionName
        testInstrumentationRunner AndroidX.AndroidJUnitRunner

        javaCompileOptions {
            annotationProcessorOptions {
                //在编译时将数据库的架构信息导出为 JSON 文件
                arguments += ["room.schemaLocation":
                                      "$projectDir/schemas".toString()]
            }
        }


//        javaCompileOptions {
//            annotationProcessorOptions {
//                arguments = [AROUTER_MODULE_NAME: project.getName()]
//                includeCompileClasspath = true
//            }
//        }


    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    //资源文件前缀限定
    resourcePrefix "common_"


    sourceSets {
        main {
            proto {
                // proto 文件默认路径是 src/main/proto
                // 可以通过 srcDir 修改 proto 文件的位置
                srcDir 'src/main/proto'
            }
        }
        // Room
        // Adds exported schema location as test app assets.
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }

}
kapt {
    arguments {
        arg("AROUTER_MODULE_NAME", project.getName())
    }
}


dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])

    api project(path: ':module_base')

    kapt GitHub.GlideCompiler
    kapt GitHub.ARouteCompiler
    kapt JetPack.RoomCompiler
//    kapt Hilt.compiler
//    kapt Hilt.daggerCompiler
}

protobuf {
    // 设置 protoc 的版本
    protoc {
        // //从仓库下载 protoc 这里的版本号需要与依赖 com.google.protobuf:protobuf-javalite:xxx 版本相同
        artifact = JetPack.ProtoBuf_Protoc
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option "lite"
                }
            }
        }
    }

    // 默认生成目录 $buildDir/generated/source/proto 通过 generatedFilesBaseDir 改变生成位置
//    generatedFilesBaseDir = "$projectDir/src/main"
}